{if;{get;_TerraTodo};==;;{set;_TerraTodo;[]}}
{if;{args};==;;
  {if;{length;{get;_TerraTodo}};==;0;{execcc;{commandname};help}{return}}
  {//; Get todo list}
  {set;~list;[]}
  {set;~num;0}
  {foreach;~line;{get;_TerraTodo};{push;~list;`{increment;~num}` - {get;~line}}}
  {set;~list;{exec;splitby;{newline};1024;{join;~list;{newline}}}}
  {set;~fields;[]}
  {push;~fields;fields.name::pencil: Todo List;fields.value:{shift;~list};fields.inline:true}
  {foreach;~line;~list;{push;~fields;fields.name:...;fields.value:{get;~line};fields.inline:true}}
  {embed;{apply;embedbuild;{get;~fields}}}
  ;
  {//; Otherwise parse args}
  {switch;{args;0}
    ;["help","h"];
    {embed;{embedbuild;fields.name::pencil: Todo List;
fields.value:{exec;use;[add] <text...>}
{exec;use;del #}
{exec;use;edit # <text...>}
{exec;use;clear}
{exec;use;help}}}
    {return}
    ;["add","a","create","c"];
    {push;{get;_TerraTodo};{args;1;n}}
    ;["remove","rem","r","delete","del","d","edit","e"];
    {if;{length;{get;_TerraTodo}};<;{args;1};:x: Invalid number.{return}}
    {switch;{args;0}
      ;["remove","rem","r","delete","del","d"];
      {void;{splice;{get;_TerraTodo};{math;-;{args;1};1};1}}
      ;["edit","e","update","u"];
      {void;{splice;{get;_TerraTodo};{math;-;{args;1};1};1;{args;2;n}}}
    }
    ;clear;
    {set;_TerraTodo;[]}
    ;
    {execcc;{commandname};add {args}}
    {return}
  }
  {reactadd;{messageid};âœ…}
}
