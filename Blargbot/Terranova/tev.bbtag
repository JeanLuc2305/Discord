{//; unterdr√ºckt Fehler vom Lookup-System}
{suppresslookup}

{//; Rolle Terranova}
{set;~roleID;536240467562725389}

{//; Setzen der Farbe f√ºr Embed}
{set;~eColor;[]}
{set;~roles;{roles;{userid}}}
{foreach;~color;{get;~roles};
  {if;{rolecolor;{get;~color}};!=;000000;
    {push;{get;~eColor};{rolecolor;{get;~color}}}}}
{if;{length;{get;~eColor}};==;0;
  {set;~eColor;c1694f}}

{//; Check ob User schon Punkte hat}
{if;{get;_Wert{userid}};<;0;{set;_Wert{userid};0}{commit}}

{//; Kein Argument -> gibt nur Punkte aus}
{if;{argslength};<=;0;{embed;{buildembed;
  color:{get;~eColor;0};
  title:Punktezahl {usernick};
  description:{get;_Wert{userid}}{rollback}}}{return}}

{//; Zeigt eine aktuelle Punkte√ºbersicht}
{set;~array;[]}
{set;~platz;1}
{if;{args};includes;show;
{embed;{buildembed;
  color:{get;~eColor;0};
  title:üèÜ Punkte√ºbersicht üèÜ;
  description:
{foreach;~user;{rolemembers;{get;~roleID}};{if;{get;_Wert{get;~user}};>;0;
{push;{get;~array};***{get;_Wert{get;~user}}*** ‚áæ {usernick;{get;~user}}}}}
{sort;{get;~array};descending}
{foreach;~value;{get;~array};
{if;{get;~platz};includes;1;:first_place: {shift;{get;~array}}{newline}{set;~platz;2};
{if;{get;~platz};includes;2;:second_place: {shift;{get;~array}}{newline}{set;~platz;3};
{if;{get;~platz};includes;3;:third_place: {shift;{get;~array}}{newline}{set;~platz;4}; 
{if;{get;~platz};includes;4;{newline}:four: {shift;{get;~array}}{newline}{set;~platz;5};
{if;{get;~platz};includes;5;:five: {shift;{get;~array}}{set;~platz;6}{newline;2};   
{shift;{get;~array}}{newline}}}}}}}
}}{return}}


{//; Setzt Punkte wieder auf 0}
{if;{args};includes;zero;
{set;_Wert{userid};0}{commit}
{embed;{buildembed;
  color:{get;~eColor;0};
  title:Hier ist deine aktuelle Punktezahl {usernick};
  description:{get;_Wert{userid}}{rollback}}}
  {modlog;Terranova Event;{userid};;Punktezahl auf 0 gesetzt.;{get;~eColor}}
  {return}}

{//; L√∂scht die Werte von Allen!}
{if;{args};includes;clear;
{set;~msg;{send;{channelid};
M√∂chtest du wirklich die Werte von allen Teilnehmern l√∂schen?}}
{//;Hinzuf√ºgen der Reaktionen}
{reactadd;{get;~msg};üëçüëé}
{//;Abwarten auf Reaktion}
{void;{waitreaction;{get;~msg};{userid};üëçüëé;{set;~reaction;{reaction}}}}
{//;Auslesen der Reaktionen}
{clean;{switch;{get;~reaction};
üëé;{embed;{buildembed;
  color:{get;~eColor;0};
  title:Befehl wurde abgebrochen.}}{return};
üëç;{foreach;~user;{rolemembers;{get;~roleID}};{if;{get;_Wert{get;~user}};>;0;
{set;_Wert{get;~user};0}{commit}}}
{embed;{buildembed;
  color:{get;~eColor;0};
  title:Die Werte von allen Teilnehmern wurden gel√∂scht.}}
  {modlog;Terranova Event;{userid};;Die Werte von allen Teilnehmern wurden gel√∂scht.;{get;~eColor}}
}}{return}}


{//; Zeigt eine aktuelle Punkte√ºbersicht}
{if;{args};includes;help;
{embed;{buildembed;
  color:{get;~eColor;0};
  title:üèÜ Terranova Event üèÜ;
  fields.name:Befehl;
  fields.value:  
1Ô∏è‚É£ {prefix}tev help
2Ô∏è‚É£ {prefix}tev
3Ô∏è‚É£ {prefix}tev 15
4Ô∏è‚É£ {prefix}tev zero
5Ô∏è‚É£ {prefix}tev show;
  fields.inline:true;
  
  fields.name:Erkl√§rung;
  fields.value:
1Ô∏è‚É£ Hilfe zum Terranova Event Befehl
2Ô∏è‚É£ Zeigt die eigenen Punkte
3Ô∏è‚É£ Z.b. Punkte die dazu gerechnet werden
4Ô∏è‚É£ Setzt Punkte auf 0 zur√ºck
5Ô∏è‚É£ Gibt ein Ergebnis bzw aktuellen Stand aus;
  fields.inline:true
  
}}
{return}}


{//; Rechnet Punkte zum vorhandenen Wert dazu}
{set;_Wert{userid};{math;+;{get;_Wert{userid}};{args}}}{commit}
{embed;{buildembed;
  color:{get;~eColor;0};
  title:Hier ist deine aktuelle Punktezahl {usernick};
  description:{get;_Wert{userid}}{rollback}}
  {modlog;Terranova Event;{userid};;Punktezahl um {args} auf {get;_Wert{userid}}{rollback} gesetzt.;{get;~eColor}}
  {return}}
  